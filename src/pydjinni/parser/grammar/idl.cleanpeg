idl             = (import_def/extern)* (type_def/namespace)* EOF
comment         = r'#.*$'+
type_def        = enum/flags/record/interface/named_function
enum            = comment? identifier '=' 'enum' '{' item* '}'
flags           = comment? identifier '=' 'flags' '{' flag* '}'
declaration     = r'[\w]+'
deriving        = 'deriving' '(' ((declaration ',')* declaration)? ')'
record          = comment? identifier '=' 'record' targets? '{' (field)* '}' deriving?
interface       = comment? identifier '=' main? 'interface' targets? '{' (method/property)* '}'
main            = 'main'
named_function  = comment? identifier '=' function ';'
function        = ('function' targets?)? '(' ((parameter ',')* parameter)? ')' ('->' type_ref)?
field           = comment? identifier ':' type_ref ';'
method          = comment? 'static'? 'const'? identifier '(' ((parameter ',')* parameter)? ')' ('->' type_ref)? ';'
parameter       = identifier ':' type_ref
targets         = (r'\+[a-z]+'/r'-[a-z]+')+
property        = comment? 'property' readonly? identifier ':' type_ref ';'
readonly        = '(' 'readonly' ')'
item            = comment? identifier ';'
flag            = comment? identifier flag_modifier? ';'
flag_modifier   = '=' ('all'/'none')
identifier      = r'[\w]+'
type_ref        = function/data_type
data_type       = r'[\.\w]+' ('<' (data_type ',')* data_type '>')? optional?
optional        = '?'
filepath        = r'".*"'
import_def      = '@import' filepath
extern          = '@extern' filepath
namespace       = begin_namespace (type_def/namespace)* end_namespace
begin_namespace = 'namespace' (identifier '.')* identifier '{'
end_namespace   = '}'
